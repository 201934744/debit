;NSIS installer for debit
;written by Jean-Baptiste Note
;Based on Basic MUI script by Joost Verburg

; these variables must be defined on the commandline
!ifndef SRCDIR
  !error "You must define the source directory"
!endif

;--------------------------------
;Include Modern UI

  !include "MUI.nsh"

;--------------------------------
;General

  ;Name and file
  Name "Ulogic's debitting tools"
  OutFile "debit-installer.exe"

  ;Default installation folder
  InstallDir "$PROGRAMFILES\debit"

  ;Get installation folder from registry if available
  InstallDirRegKey HKCU "Software\debit" ""

;-------------------------------
; MUI icons
  !define MUI_ICON "${SRCDIR}\desktop\artwork\ulogic_installer.ico"
  !define MUI_UNICON "${SRCDIR}\desktop\artwork\ulogic_uninstaller.ico"

;--------------------------------
;Interface Settings

  !define MUI_HEADERIMAGE
  !define MUI_HEADERIMAGE_RIGHT
  !define MUI_HEADERIMAGE_BITMAP "${SRCDIR}\desktop\artwork\ulogic_banner.bmp"
  !define MUI_ABORTWARNING

;--------------------------------
;Variables

  Var STARTMENU_FOLDER
  Var MUI_TEMP

;--------------------------------
;Functions

Function .onInit
	InitPluginsDir
	File /oname=$PLUGINSDIR\splash.bmp "${SRCDIR}\desktop\artwork\ulogic_splash.bmp"
	splash::show 3000 $PLUGINSDIR\splash
	Pop $0
FunctionEnd

;--------------------------------
;Pages

  !insertmacro MUI_PAGE_WELCOME
  !insertmacro MUI_PAGE_LICENSE "${SRCDIR}/COPYING"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY

  ;Start Menu Folder Page Configuration
  !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU"
  !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\debit"
  !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"

  !insertmacro MUI_PAGE_STARTMENU Application $STARTMENU_FOLDER

  !insertmacro MUI_PAGE_INSTFILES

  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES

;--------------------------------
;Languages

  !insertmacro MUI_LANGUAGE "English"

;--------------------------------
;Installer Sections

Section "Debitting graphical tools" SecDisplay
  DetailPrint "Installing debitting graphical tools..."

  SetOutPath "$INSTDIR\bin"
  File ..\xiledit\xiledit.exe

; xiledit pixmaps
  SetOutPath "$INSTDIR\data\pixmaps"
  File "${SRCDIR}\desktop\artwork\ulogic_icon_32.png"

  SetOutPath "$INSTDIR\gfx"
  File "${SRCDIR}\desktop\artwork\ulogic_icon.ico"

SectionEnd

Section "Debitting database" SecDatabase
  DetailPrint "Installing debitting database..."
  SetOutPath "$INSTDIR\data"
  !include "xildb.nsh"
SectionEnd

Section "Debitting shell tools" SecXilinx

  DetailPrint "Installing debitting shell tools..."
  SetOutPath "$INSTDIR\bin"

  File ..\altera\altera.exe
  File ..\utils\correlation\bitisolation.exe
  File ..\utils\diff_bin_pos.exe
  File ..\debit.exe
  File ..\bit2pdf.exe

  DetailPrint "Installing debitting .bat helper files..."
  SetOutPath "$INSTDIR"
  File ${SRCDIR}\windows\scripts\bit2pdf.bat
  File ${SRCDIR}\windows\scripts\bit2luts.bat
  File ${SRCDIR}\windows\scripts\bit2bram.bat
  File ${SRCDIR}\windows\scripts\bit2pips.bat

  ;Store installation folder
  WriteRegStr HKCU "Software\debit" "" $INSTDIR

  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"

SectionEnd

Section "StartMenu entries" SecStartMenu
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    CreateDirectory "$SMPROGRAMS\$STARTMENU_FOLDER"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Xiledit.lnk" "$INSTDIR\bin\xiledit.exe" '"-d" "$INSTDIR\data"' "$INSTDIR\gfx\ulogic_icon.ico"
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section "File type and context menus" SecCtx
  ; Register icons for the files
  SetOutPath "$INSTDIR\gfx"
  File "${SRCDIR}\desktop\artwork\application-x-xilinx-bitstream.ico"
  File "${SRCDIR}\desktop\artwork\application-x-altera-bitstream.ico"

  ; create file associations, test before if needed
  DetailPrint "creating file associations"
  ClearErrors
  ReadRegStr $0 HKCR ".bit" ""
  StrCmp $0 "" 0 +4
    WriteRegStr HKCR ".bit" "" "debit.bitfile"
    WriteRegStr HKCR "debit.bitfile" "" "Xilinx Bitstream file"
    WriteRegStr HKCR "debit.bitfile\DefaultIcon" "" "$INSTDIR\gfx\application-x-xilinx-bitstream.ico"
  ReadRegStr $0 HKCR ".sof" ""
  StrCmp $0 "" 0 +4
    WriteRegStr HKCR ".sof" "" "soffile"
    WriteRegStr HKCR "debit.soffile" "" "Altera Bitstream file"
    WriteRegStr HKCR "debit.soffile\DefaultIcon" "" "$INSTDIR\gfx\application-x-altera-bitstream.ico"
  IfErrors 0 +2
     DetailPrint "Problem creating file associations for bitstream files"

  DetailPrint "Registering default browser for Xilinx bitstreams"
  ClearErrors
  ReadRegStr $0 HKCR ".bit" ""
  WriteRegStr HKCR "$0\shell\edit" "" "Browse bitstream file"
  WriteRegStr HKCR "$0\shell\edit\command" "" '"$INSTDIR\bin\xiledit.exe" "-d" "$INSTDIR\data" "-i" "%1"'
;  ReadRegStr $0 HKCR ".sof" ""
;  WriteRegStr HKCR "$0\shell\edit\command" "" '"$INSTDIR\bin\altedit.exe" "%1"'
  IfErrors 0 +2
     DetailPrint "Error creating default browser"

  DetailPrint "Creating context menu entries for debit"
  ClearErrors
  ReadRegStr $0 HKCR ".bit" ""
  WriteRegStr HKCR "$0\shell\x_pdf" "" "Convert to PDF"
  WriteRegStr HKCR "$0\shell\x_pdf\command" "" '"$INSTDIR\bit2pdf.bat" "%1"'
  WriteRegStr HKCR "$0\shell\y_pips" "" "Extract PIPs"
  WriteRegStr HKCR "$0\shell\y_pips\command" "" '"$INSTDIR\bit2pips.bat" "%1"'
  WriteRegStr HKCR "$0\shell\y_bram" "" "Extract BRAM data"
  WriteRegStr HKCR "$0\shell\y_bram\command" "" '"$INSTDIR\bit2bram.bat" "%1"'
  WriteRegStr HKCR "$0\shell\y_lut" "" "Extract LUT data"
  WriteRegStr HKCR "$0\shell\y_lut\command" "" '"$INSTDIR\bit2luts.bat" "%1"'
  IfErrors 0 +2
     DetailPrint "Error creating custom actions"

  DetailPrint "Signalling the change to windows"
  System::Call 'Shell32::SHChangeNotify(i 0x8000000, i 0, i 0, i 0)'

SectionEnd

;--------------------------------
;Descriptions

  ;Language strings
  LangString DESC_SecXilinx ${LANG_ENGLISH} "Command-line utilities for debitting"
  LangString DESC_SecDisplay ${LANG_ENGLISH} "Graphical utilities for debitting"
  LangString DESC_SecDatabase ${LANG_ENGLISH} "Database for the Xilinx xc2v2000 chip"
  LangString DESC_SecCtx ${LANG_ENGLISH} "File type and context menus"
  LangString DESC_SecStartMenu ${LANG_ENGLISH} "StartMenu entries"

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${SecXilinx} $(DESC_SecXilinx)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecDisplay} $(DESC_SecDisplay)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecDatabase} $(DESC_SecDatabase)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecCtx} $(DESC_SecCtx)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecStartMenu} $(DESC_SecStartMenu)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------
;Uninstaller Section

Section "Uninstall"

  ;; Data files deletion

  Delete "$INSTDIR\bin\xiledit.exe"
  Delete "$INSTDIR\data\pixmaps\ulogic_icon_32.png"
  RMDir "$INSTDIR\data\pixmaps"

  Delete "$INSTDIR\gfx\application-x-xilinx-bitstream.ico"
  Delete "$INSTDIR\gfx\application-x-altera-bitstream.ico"
  Delete "$INSTDIR\gfx\ulogic_icon.ico"
  RMDir "$INSTDIR\gfx"

  Delete "$INSTDIR\bin\altera.exe"
  Delete "$INSTDIR\bin\bitisolation.exe"
  Delete "$INSTDIR\bin\diff_bin_pos.exe"
  Delete "$INSTDIR\bin\debit.exe"
  Delete "$INSTDIR\bin\bit2pdf.exe"
  RMDir "$INSTDIR\bin"

  RMDir /r "$INSTDIR\data"

  Delete "$INSTDIR\Uninstall.exe"

  Delete "$INSTDIR\bit2pdf.bat"
  Delete "$INSTDIR\bit2luts.bat"
  Delete "$INSTDIR\bit2bram.bat"
  Delete "$INSTDIR\bit2pips.bat"
  RMDir "$INSTDIR"

  ;; Start Menu deletion

  !insertmacro MUI_STARTMENU_GETFOLDER Application $MUI_TEMP

  Delete "$SMPROGRAMS\$MUI_TEMP\Xiledit.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\Uninstall.lnk"

  ;Delete empty start menu parent diretories

  DetailPrint "Removing startmenu entries..."
  StrCpy $MUI_TEMP "$SMPROGRAMS\$MUI_TEMP"

  startMenuDeleteLoop:
	ClearErrors
    RMDir $MUI_TEMP
    GetFullPathName $MUI_TEMP "$MUI_TEMP\.."

    IfErrors startMenuDeleteLoopDone

    StrCmp $MUI_TEMP $SMPROGRAMS startMenuDeleteLoopDone startMenuDeleteLoop
  startMenuDeleteLoopDone:

  DeleteRegKey /ifempty HKCU "Software\debit"

SectionEnd
